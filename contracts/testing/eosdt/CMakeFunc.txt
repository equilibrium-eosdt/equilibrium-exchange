function(git_init git_branch git_commit git_version)
    execute_process(COMMAND git rev-parse --abbrev-ref HEAD
            WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
            OUTPUT_VARIABLE GIT_BRANCH
            OUTPUT_STRIP_TRAILING_WHITESPACE
            )
    set(${git_branch} ${GIT_BRANCH} PARENT_SCOPE)
    execute_process(COMMAND git log -1 --format=%h
            WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
            OUTPUT_VARIABLE GIT_COMMIT
            OUTPUT_STRIP_TRAILING_WHITESPACE
            )
    set(${git_commit} ${GIT_COMMIT} PARENT_SCOPE)
    execute_process(COMMAND git log -1 --format=%cd --date=local
            WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
            OUTPUT_VARIABLE GIT_VERSION
            OUTPUT_STRIP_TRAILING_WHITESPACE
            )
    set(${git_version} ${GIT_VERSION} PARENT_SCOPE)
endfunction()

function(get_file_size var filename)
    if(EXISTS "${filename}")
        file(READ "${filename}" content HEX)
        string(LENGTH "${content}" content_length)
        math(EXPR content_length "${content_length} / 2")
        set(${var} ${content_length} PARENT_SCOPE)
    else()
        set(${var} 0 PARENT_SCOPE)
    endif()
endfunction()

function(get_count_line_of_code var filenames)
    set(cloc 0)
    foreach(filename ${filenames})
        file(STRINGS "${filename}" strings)
        list(LENGTH strings count)
        math(EXPR cloc "${cloc}+${count}")
    endforeach(filename)
    set(${var} ${cloc} PARENT_SCOPE)
endfunction()


